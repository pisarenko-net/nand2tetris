class World {
	field Array cells;

	constructor World new() {
		let cells = Array.new(64);
		do clear();
		return this;
	}

	method void dispose() {
		var int currentRow;
		var Array row;

		let currentRow = 0;

		while (currentRow < 64) {
			let row = cells[currentRow];
			do row.dispose();
			let currentRow = currentRow + 1;
		}

		do cells.dispose();
		do Memory.deAlloc(this);
		return;
   }

	method void clear() {
		var int currentRow, currentColumn;
		var Array row;

		let currentRow = 0;

		while (currentRow < 64) {
			let row = Array.new(128);

			let currentColumn = 0;
			while (currentColumn < 128) {
				let row[currentColumn] = false;
				let currentColumn = currentColumn + 1;
			}

			let cells[currentRow] = row;
			let currentRow = currentRow + 1;
		}
		return;
	}

	method void transformToNextGeneration() {
		var List pendingChanges, pendingChangesPrevious;
		var int currentRow, currentColumn;
		var Array row;

		let currentRow = 0;

		let pendingChanges = List.new(-1, null);
		let pendingChangesPrevious = List.new(-1, null);

		while (currentRow < 64) {
			if (areChangesPending(pendingChangesPrevious)) {
				do applyChanges(currentRow - 2, pendingChangesPrevious);
			}
			do pendingChangesPrevious.dispose();
			let pendingChangesPrevious = pendingChanges;
			let pendingChanges = List.new(-1, null);

			let row = cells[currentRow];
			let currentColumn = 0;

			while (currentColumn < 128) {
				if (shouldCellToggle(currentRow, currentColumn)) {
					let pendingChanges = List.new(currentColumn, pendingChanges);
				}
				let currentColumn = currentColumn + 1;
			}
			let currentRow = currentRow + 1;
		}

		do applyChanges(currentRow - 1, pendingChanges);
		do applyChanges(currentRow - 2, pendingChangesPrevious);

		do pendingChanges.dispose();
		do pendingChangesPrevious.dispose();

		return;
	}

	method boolean areChangesPending(List changedColumns) {
		if (changedColumns.isEmpty()) {
			return false;
		} else {
			return true;
		}
	}

	method void applyChanges(int row, List changedColumns) {
		var List currentColumnElement;
		var int currentColumn;

		let currentColumnElement = changedColumns;
		let currentColumn = currentColumnElement.getData();

		while (~(currentColumn = -1)) {
			do toggleCell(row, currentColumn);
			let currentColumnElement = currentColumnElement.getNext();
			let currentColumn = currentColumnElement.getData();
		}
		return;
	}

	method void toggleCell(int rowNumber, int columnNumber) {
		var boolean currentValue;
		var Array row;

		let row = cells[rowNumber];
		let currentValue = row[columnNumber];
		let currentValue = ~currentValue;
		let row[columnNumber] = currentValue;

		return;
	}

	method boolean shouldCellToggle(int row, int column) {
		var int neighbours;
		let neighbours = countNeighbours(row, column);

		if (isCellAlive(row, column)) {
			if ((neighbours < 2) | (neighbours > 3)) {
				return true;
			} else {
				return false;
			}
		} else {
			if (neighbours = 3) {
				return true;
			} else {
				return false;
			}
		}
	}

	method boolean isCellAlive(int row, int column) {
		var Array row;
		var boolean currentValue;
		let row = cells[row];
		let currentValue = row[column];
		return currentValue;
	}

	method int countNeighbours(int row, int column) {
		var int neighbours;
		let neighbours = 0;

		if (row > 0) {
			// north
			if (isCellAlive(row-1, column)) {
				let neighbours = neighbours + 1;
			}
			// NW
			if ((column > 0) & (isCellAlive(row-1, column-1))) {
				let neighbours = neighbours + 1;
			}
			// NE
			if ((column < 127) & (isCellAlive(row-1, column+1))) {
				let neighbours = neighbours + 1;
			}
		}
		// west
		if ((column > 0) & (isCellAlive(row, column-1))) {
			let neighbours = neighbours + 1;
		}
		// east
		if ((column < 127) & (isCellAlive(row, column+1))) {
			let neighbours = neighbours + 1;
		}
		if (row < 63) {
			// south
			if (isCellAlive(row+1, column)) {
				let neighbours = neighbours + 1;
			}
			// SW
			if ((column > 0) & isCellAlive(row+1, column-1)) {
				let neighbours = neighbours + 1;
			}
			// SE
			if ((column < 127) & isCellAlive(row+1, column+1)) {
				let neighbours = neighbours + 1;
			}
		}

		return neighbours;
	}
}